grammar org.xtext.browserautomation.mydsl.BrowserTest with org.eclipse.xtext.common.Terminals

generate browserTest "http://www.xtext.org/browserautomation/mydsl/BrowserTest"

TestFile:
    task+=Task*;

Task:
    '[TASK]' name=(ID | STRING)
    actions+=Action*;

Action:
    GoTo | Select | ActionCommand | Test;

GoTo:
    '[GOTO]' url=STRING;

Select:
    '[SELECT]' var=Variable
    properties+=Property*;
    
Variable:
	'$'name=ID;
	
Entree:
	var=Variable'.'(param=Attribute | untyped=ID)
	| string=STRING
	| '$'accessor=Accessor;

Property:
    '[CONTENT]' 			content=Entree |
    '[LABEL]' 				label=Entree |
    '[VALUE]' 				value=Entree |
    '[ALT]' 				alt=Entree |
    '[TYPE]' 				'$'type=Type |
    '[NAME]' 				name=STRING |
    '[NTH-CHILD]' 			child=INT |
	{Property} '[PARENT]'	'{' subProperties+=Property* '}';

ActionCommand:
    '[ACTION]' var+=Variable+
    	command=ActionType;

ActionType:
    {ActionType} 	'[CLICK]' |
    '[CHECK]' 		'$'value=Boolean |
    '[SET-TEXT]'	text=Entree |
    '[CHOOSE]' 		option=Entree;
    
Test:
    '[TEST]' var=Variable;

enum Type:
    LINK | INPUT | TEXT | CHECKBOX | DIV | TITLE | CONTENT;

enum Boolean:
	FALSE | TRUE;

enum Attribute:
	LINK | TITLE | CONTENT;

enum Accessor:
	ALL;