grammar org.xtext.browserautomation.mydsl.BrowserTest with org.eclipse.xtext.common.Terminals

generate browserTest "http://www.xtext.org/browserautomation/mydsl/BrowserTest"

TestFile:
    task+=Task*;

Task:
    '[TASK]' name=(ID | STRING)
    actions+=Action*;

Action:
    GoTo | Select | ActionCommand | Test;

GoTo:
    '[GOTO]' url=STRING;

Select:
    '[SELECT]' var=Variable
    properties+=Property*;
    
Variable:
	'$'name=ID;
	
Entree:
	var=Variable'.'(param=Attribute | untyped=ID)
	| string=STRING
	| '$'accessor=Accessor;

Property:
    prop='[CONTENT]' 			content=Entree |
    prop='[LABEL]' 				label=Entree |
    prop='[VALUE]' 				value=Entree |
    prop='[ALT]' 				alt=Entree |
    prop='[LINK]'				link=Entree |
    prop='[TYPE]' 				'$'type=Type |
    prop='[NAME]' 				name=STRING |
    prop='[NTH-CHILD]' 			child=INT |
	{Property} prop='[PARENT]'	'{' subProperties+=Property* '}';

ActionCommand:
    '[ACTION]' var=Variable
    	command=ActionType;

ActionType:
    {ActionType} 	
    action='[CLICK]' 		|
    action='[CHECK]' 		'$'value=Boolean |
    action='[SET-TEXT]'	text=Entree |
    action='[CHOOSE]' 		option=Entree;
    
Test:
    '[TEST]' var=Variable;

enum Type:
    LINK | INPUT | TEXT | CHECKBOX | DIV | SELECT | CONTENT;

enum Boolean:
	FALSE | TRUE;

enum Attribute:
	LINK | TITLE | CONTENT;

enum Accessor:
	ALL;